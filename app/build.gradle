/*
 * Mr.Mantou - On the importance of taste
 * Copyright (C) 2015-2016  XiNGRZ <xxx@oxo.ooo>
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git tag'.execute([], project.rootDir).text.trim().split("\n").last()
def gitCommits = Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "ooo.oxo.mr"

        minSdkVersion 16

        targetSdkVersion 23

        versionCode 10200 + gitCommits
        versionName gitTag

        addManifestPlaceholders APP_NAME: '@string/app_name'

        // Versioning
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        println "Building ${gitTag} (rev. ${gitCommits} on ${gitSha})"

        // FIR
        buildConfigField "boolean", "FIR_ENABLED", "!DEBUG && ${project.hasProperty('FIR_API_TOKEN')}"
        buildConfigField "String", "FIR_API_TOKEN",
                project.hasProperty('FIR_API_TOKEN') ? "\"${FIR_API_TOKEN}\"" : "null"

        // LeanCloud
        buildConfigField "String", "AVOS_APP_ID",
                project.hasProperty('AVOS_APP_ID') ? "\"${AVOS_APP_ID}\"" : "null"
        buildConfigField "String", "AVOS_APP_KEY",
                project.hasProperty('AVOS_APP_KEY') ? "\"${AVOS_APP_KEY}\"" : "null"
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    if (project.hasProperty('STOREFILE')) {
        signingConfigs {
            release {
                storeFile file(STOREFILE)
                storePassword STOREPASS
                keyAlias KEYALIAS
                keyPassword KEYPASS
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            addManifestPlaceholders APP_NAME: '@string/app_name_debug'
        }

        release {
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (project.hasProperty('STOREFILE')) {
                println "using release keystore ${signingConfigs.release.storeFile}"
                signingConfig signingConfigs.release
            } else {
                println "using default key"
            }
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            if (output.outputFile != null && output.outputFile.name.endsWith('.apk')) {
                output.outputFile = file("${output.outputFile.parent}/Mr.Mantou" +
                        "-${variant.buildType.name.toLowerCase()}" +
                        "-${variant.versionName}" +
                        ".apk")
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:design:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'

    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'io.reactivex:rxandroid:1.1.0'

    compile 'com.trello:rxlifecycle:0.4.0'
    compile 'com.trello:rxlifecycle-components:0.4.0'

    compile 'com.jakewharton.rxbinding:rxbinding-support-v4:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-design:0.4.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.4.0'

    compile 'com.tbruyelle.rxpermissions:rxpermissions:0.6.0@aar'

    compile 'com.github.oxoooo:pull-back-layout:1.0.1'
    compile 'com.github.oxoooo:touch-image-view:1.0.1'
    compile 'com.github.oxoooo:data-binding-support:1.1.0'

    // required by fir
    compile 'com.squareup.okhttp:okhttp:2.7.2'

    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'im.fir:sdk:latest.integration@aar'
}
